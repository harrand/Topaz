# Level 0 #

function(topaz_add_library)
	cmake_parse_arguments(
		TOPAZ_ADD_LIBRARY
		"DEFINE_PACKAGE"
		"TARGET"
		"SOURCES;SHADERS;TEXT_FILES;BUNDLE_FILES"
		${ARGN}
	)

	add_library(${TOPAZ_ADD_LIBRARY_TARGET} STATIC
		${TOPAZ_ADD_LIBRARY_SOURCES}
	)
	topaz_add_shader(
		TARGET ${TOPAZ_ADD_LIBRARY_TARGET}
		SHADERS
			${TOPAZ_ADD_LIBRARY_SHADERS}
	)
	add_text(
		TARGET ${TOPAZ_ADD_LIBRARY_TARGET}
		INPUT_DIR ${PROJECT_SOURCE_DIR}
		OUTPUT_DIR ${PROJECT_BINARY_DIR}
		TEXT_FILES ${TOPAZ_ADD_LIBRARY_TEXT_FILES}
	)
	if(TOPAZ_ADD_LIBRARY_BUNDLE_FILES)
		topaz_bundle_files(
			TARGET ${TOPAZ_ADD_LIBRARY_TARGET}
			FILES ${TOPAZ_ADD_LIBRARY_BUNDLE_FILES}
		)
	endif()
	if(TOPAZ_ADD_LIBRARY_DEFINE_PACKAGE)
		topaz_define_package(TARGET ${TOPAZ_ADD_LIBRARY_TARGET})
	endif()
endfunction()

function(topaz_add_executable)
	cmake_parse_arguments(
		TOPAZ_ADD_EXECUTABLE
		"DEFINE_PACKAGE"
		"TARGET;FAVICON"
		"SOURCES;SHADERS;TEXT_FILES;BUNDLE_FILES"
		${ARGN}
	)

	if(${CMAKE_BUILD_TYPE} MATCHES "release")
	add_executable(${TOPAZ_ADD_EXECUTABLE_TARGET} WIN32
		${TOPAZ_ADD_EXECUTABLE_SOURCES}
	)
	else()
	add_executable(${TOPAZ_ADD_EXECUTABLE_TARGET}
		${TOPAZ_ADD_EXECUTABLE_SOURCES}
	)
	endif()
	topaz_add_shader(
		TARGET ${TOPAZ_ADD_EXECUTABLE_TARGET}
		SHADERS
			${TOPAZ_ADD_EXECUTABLE_SHADERS}
	)
	add_text(
		TARGET ${TOPAZ_ADD_EXECUTABLE_TARGET}
		INPUT_DIR ${PROJECT_SOURCE_DIR}
		OUTPUT_DIR ${PROJECT_BINARY_DIR}
		TEXT_FILES ${TOPAZ_ADD_EXECUTABLE_TEXT_FILES}
	)
	if(TOPAZ_ADD_EXECUTABLE_BUNDLE_FILES)
		topaz_bundle_files(
			TARGET ${TOPAZ_ADD_EXECUTABLE_TARGET}
			FILES ${TOPAZ_ADD_EXECUTABLE_BUNDLE_FILES}
		)
	endif()
	if(TOPAZ_ADD_EXECUTABLE_DEFINE_PACKAGE)
		topaz_define_package(TARGET ${TOPAZ_ADD_EXECUTABLE_TARGET})
	endif()

	if(TOPAZ_ADD_EXECUTABLE_FAVICON)
		if(WIN32)
			set(rc_name "${CMAKE_BINARY_DIR}/temp/favicon_${TOPAZ_ADD_EXECUTABLE_TARGET}.rc")
			file(MAKE_DIRECTORY "${CMAKE_BINARY_DIR}/temp")
			file(RELATIVE_PATH REL_FAVICON_PATH ${CMAKE_BINARY_DIR} ${CMAKE_SOURCE_DIR}/${TOPAZ_ADD_EXECUTABLE_FAVICON})
			set(ICON_ID 101)
			add_custom_command(
				OUTPUT ${rc_name}
				COMMAND ${CMAKE_COMMAND} -E echo "#define IDI_ICON1 ${ICON_ID}" > ${rc_name}
				COMMAND ${CMAKE_COMMAND} -E echo "IDI_ICON1 ICON DISCARDABLE \"${REL_FAVICON_PATH}\"" >> ${rc_name}
				DEPENDS ${TOPAZ_ADD_EXECUTABLE_FAVICON}
			)
			target_sources(${TOPAZ_ADD_EXECUTABLE_TARGET} PUBLIC ${rc_name})
			target_compile_definitions(${TOPAZ_ADD_EXECUTABLE_TARGET} PRIVATE "-DTZ_CUSTOM_ICON_ID=${ICON_ID}")
		endif()
	endif()
endfunction()