shader(type = vertex);

#define MAX_TEX_COUNT 8
#define WEIGHT4_COUNT 1

struct vertex_t
{
	vec3 position;
	vec4 texcoord4[MAX_TEX_COUNT];
	vec3 normal;
	vec3 tangent;
	uvec4 joint_indices[WEIGHT4_COUNT];
	vec4 joint_weights[WEIGHT4_COUNT];
};

struct texture_locator
{
	vec3 colour_tint;
	uint texture_id;
};

struct object_t
{
	mat4 global_transform;
	mat4 inverse_bind_matrix;
	vec3 colour_tint;
	texture_locator[MAX_TEX_COUNT] textures;
	uvec2 joint_span;
	uvec2 unused;
};

resource(id = 0) const buffer vertex_buffer
{
	vertex_t data[];
} vertices;

resource(id = 1) const buffer object_buffer
{
	object_t data[];
} objects;

resource(id = 2) const buffer camera_buffer
{
	mat4 view;
	mat4 projection;
} camera;

resource(id = 3) const buffer joint_object_id_buffer
{
	uint object[];
} joint_to;

output(id = 0) vec2 out::texcoord[MAX_TEX_COUNT];
output(id = 9) vec3 out::normal;
output(id = 10) vec3 out::colour;
output(id = 11) texture_locator out::textures[MAX_TEX_COUNT];

mat4 obj_get_joint_matrix(uint object_id)
{
	object_t o = objects.data[object_id];
	return o.global_transform * o.inverse_bind_matrix;
}

mat4 get_joint_matrix(object_t o, uint joint_id)
{
	uvec2 joint_span = o.joint_span;
	uint joint_array_offset = joint_span.x;
	uint object_id = joint_to.object[joint_array_offset + joint_id];
	return obj_get_joint_matrix(object_id);
}

void main()
{
	vertex_t vtx = vertices.data[in::vertex_id];
	object_t obj = objects.data[in::draw_id + in::base_instance];	

	mat4 skin_mat =
		vtx.joint_weights[0].x * get_joint_matrix(obj, vtx.joint_indices[0].x) +
		vtx.joint_weights[0].y * get_joint_matrix(obj, vtx.joint_indices[0].y) +
		vtx.joint_weights[0].z * get_joint_matrix(obj, vtx.joint_indices[0].z) +
		vtx.joint_weights[0].w * get_joint_matrix(obj, vtx.joint_indices[0].w);
	if(vtx.joint_weights[0] == vec4(0.0))
	{
		// it's probably not a skinned mesh.
		skin_mat = obj.global_transform;
	}
	out::position = camera.projection * camera.view * skin_mat * vec4(vtx.position, 1.0f);

	out::normal = vtx.normal;
	out::colour = obj.colour_tint;
	for(uint i = 0; i < MAX_TEX_COUNT; i++)
	{
		out::texcoord[i] = vtx.texcoord4[i].xy;
		out::textures[i] = obj.textures[i];
	}
}